ext.moduleName = 'com.tngtech.archunit.junit5.api'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    compile project(path: ':archunit')
    compile project(path: ':archunit-junit', configuration: 'api')
    compile dependency.junitPlatformCommons
}

javadoc {
    source(project(':archunit-junit').sourceSets.api.allJava)
}
sourcesJar {
    from project(':archunit-junit').sourceSets.api.allSource
}

shadowJar {
    exclude 'META-INF/maven/**'

    dependencies {
        exclude(project(':archunit'))
        exclude(dependency { it.name != dependency.guava && !it.name.contains('archunit-junit') })
    }
}

def configureDependencies = { pom ->
    pom.dependencies.removeAll {
        it.scope != 'compile' || !(it.artifactId in ['archunit'])
    }
    pom.dependencies.find { it.artifactId == 'archunit' }.classifier = null
}
this.with project(':archunit-junit').configureJUnitArchive(configureDependencies)