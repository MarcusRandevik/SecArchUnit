buildscript {
    repositories {
        mavenCentral()
    }
}

subprojects {
    ext.repackagesAsm = false

    afterEvaluate {
        tasks.withType(Jar) {
            manifest {
                attributes(
                        'Built-By': "Gradle ${gradle.gradleVersion}",
                        'Built-Date': new Date().format('yyyy-MM-dd'),
                        'Specification-Title': createModuleDescription(app.name, project),
                        'Specification-Version': version,
                        'Specification-Vendor': company.name,
                        'Implementation-Title': "com.tngtech.${project.name.replace('-', '.')}",
                        'Implementation-Version': version,
                        'Implementation-Vendor': company.name,
                        'Issue-Tracker': app.urls.issues,
                        'Documentation-URL': app.urls.doc,
                        'Copyright': "${year} ${company.name}",
                        'License': app.license.name,
                        'Automatic-Module-Name': moduleName
                )
            }
        }
    }
}

productionProjects*.with {
    apply plugin: 'com.github.johnrengelman.shadow'

    shadowJar {
        classifier = ''

        exclude 'org/apache/**'
        exclude 'Log4j*'
        exclude 'module-info.class'

        relocate 'org.objectweb.asm', asmRelocationPackage
        relocate 'com.google', guavaRelocationPackage
    }
    jar.enabled = false

    task finishArchive(dependsOn: shadowJar) {}
    build.dependsOn(finishArchive)
}